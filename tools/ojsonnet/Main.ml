(* Yoann Padioleau
 *
 * Copyright (C) 2023 Semgrep Inc.
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public License
 * version 2.1 as published by the Free Software Foundation, with the
 * special exception on linking described in file LICENSE.
 *
 * This program is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the file
 * LICENSE for more details.
 *)
open Common
module Arg = Cmdliner.Arg
module Cmd = Cmdliner.Cmd
module Term = Cmdliner.Term

(*****************************************************************************)
(* Prelude *)
(*****************************************************************************)
(* Jsonnet interpreter written in OCaml.
 *
 * For more information see libs/ojsonnet/
 *)

(*****************************************************************************)
(* Types *)
(*****************************************************************************)

type format = JSON | YAML [@@deriving show]

type conf = { target : Fpath.t; format : format; common : CLI_common.conf }
[@@deriving show]

(*****************************************************************************)
(* CLI flags *)
(*****************************************************************************)
let o_target : string Term.t =
  let info = Arg.info [] ~docv:"TARGET" ~doc:"File to interpret" in
  Arg.value (Arg.pos 0 Arg.string "default.jsonnet" info)

let o_yaml : bool Term.t =
  let info = Arg.info [ "yaml" ] ~doc:"Generate YAML instead of JSON" in
  Arg.value (Arg.flag info)

(* this used to be autogenerated by ppx_deriving_cmdliner *)
let term : conf Term.t =
  let combine common target yaml =
    { common; target = Fpath.v target; format = (if yaml then YAML else JSON) }
  in
  Term.(const combine $ CLI_common.o_common $ o_target $ o_yaml)

(*****************************************************************************)
(* Entry point *)
(*****************************************************************************)

let rec yojson_to_yaml_value (json : Yojson.Basic.t) : Yaml.value =
  match json with
  | `Assoc xs -> `O (xs |> List.map (fun (s, t) -> (s, yojson_to_yaml_value t)))
  | `List xs -> `A (xs |> List.map yojson_to_yaml_value)
  | `String s -> `String s
  | `Int i -> `Float (float_of_int i)
  | `Bool b -> `Bool b
  | `Float f -> `Float f
  | `Null -> `Null

let interpret (file : Fpath.t) : JSON.t =
  let ast = Parse_jsonnet.parse_program file in
  let core = Desugar_jsonnet.desugar_program file ast in
  let value_ = Eval_jsonnet_subst.eval_expr core in
  let json = Eval_jsonnet_subst.manifest_value value_ in
  json

let run (conf : conf) =
  CLI_common.setup_logging ~force_color:true ~level:conf.common.logging_level;
  Logs.debug (fun m -> m "conf = %s" (show_conf conf));
  let json = interpret conf.target in
  match conf.format with
  | JSON ->
      let str = JSON.string_of_json json in
      pr str
  | YAML ->
      let y = JSON.to_yojson json in
      let v = yojson_to_yaml_value y in
      let str = Yaml.to_string_exn v in
      pr str

(*****************************************************************************)
(* Cmdliner boilerplate *)
(*****************************************************************************)

let main () =
  let info = Cmd.info Sys.argv.(0) in
  let term = Term.(const run $ term) in
  let cmd = Cmd.v info term in
  exit (Cmd.eval cmd)

let () = main ()
